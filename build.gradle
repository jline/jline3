buildscript {
  repositories {
    maven {
      url 'https://plugins.gradle.org/m2/'
    }
  }
  dependencies {
    classpath 'net.researchgate:gradle-release:2.3.4'
  }
}

apply plugin: 'net.researchgate.release'

ext.isReleaseVersion = !version.endsWith("SNAPSHOT")

apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'maven'
apply plugin: 'signing'
apply plugin: 'eclipse'

repositories {
    mavenCentral()
}

compileJava {
  sourceCompatibility = 1.8
  targetCompatibility = 1.8
}


task wrapper(type: Wrapper) {
  gradleVersion = '2.11'
}


dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.11'
    compile 'org.fusesource.jansi:jansi:1.11'
    compile 'jline:jline:2.12'
}

javadoc {
    options.encoding = 'UTF-8'
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}


// Fat-jar
task fatJar(type: Jar, dependsOn: [':compileJava', ':processResources']) {
  from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
  from files(sourceSets.main.output.classesDir)
  from files(sourceSets.main.resources)
  baseName = project.name + '-all'
  manifest {
      attributes 'Implementation-Title': 'ConsoleUI',
                 'Implementation-Version': version,
                 'Main-Class': 'de.codeshelf.consoleui.Basic'

    }
}

task printEnv {
  println "username "+sonatypeUsername
}

artifacts {
    archives javadocJar, sourcesJar
}


signing {
  required { isReleaseVersion && gradle.taskGraph.hasTask("uploadArchives") }

  sign configurations.archives
}


group = "de.codeshelf.consoleui"

if (project.hasProperty('release')) {
  uploadArchives {
    repositories {
      mavenDeployer {
        beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }
        /* just for testing
        repository(url: "file://localhost/${rootDir}/../myRepo/")
        */

        repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
          authentication(userName: sonatypeUsername, password: sonatypePassword)
        }

        snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
          authentication(userName: sonatypeUsername, password: sonatypePassword)
        }

        pom.project {
          name 'ConsoleUI'
          packaging 'jar'
          description 'Library for Ansi-Console user interaction'
          url 'https://github.com/awegmann/consoleui/'

          licenses {
            license {
              name 'The Apache Software License, Version 2.0'
              url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
              distribution 'repo'
            }
          }

          developers {
            developer {
              id "an10nn";
              name "Andreas Wegmann";
              email "andreas.we9mann@gmail.com";
            }
          }

          scm {
            connection 'scm:git:git://github.com/awegmann/consoleui.git'
            developerConnection 'scm:git:ssh://git@github.com:awegmann/consoleui.git'
            url 'https://github.com/awegmann/consoleui/'
          }
        }
      }
    }
  }
}
